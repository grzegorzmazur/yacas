/* book2TeX - convert Yacas docs back to TeX format (Serge Winitzki)
	version 0.2 */

/* If one of these occurs inside an HtmlTerm block, it must be made \\verb| |*/
TexCharsToEscape := { "{", "}", "#", "_", "&", "\\", "^", "%", "$", "~"};

Book(x) := [	/* this is the first declaration in a file */
	WriteString("
\\documentclass[twocolumn,10pt]{report}

\\setlength{\\topmargin}{-1.2in}
\\setlength{\\oddsidemargin}{-0.5in}
\\setlength{\\evensidemargin}{-0.5in}
\\setlength{\\textwidth}{7.2in}
\\setlength{\\textheight}{10.0in}

\\begin{document}
\\title{" : x : "}
\\author{by the \\textsc{Yacas} team}
\\date{\\textsc{Yacas} version: " : Version() : "}
\\maketitle
\\tableofcontents
");
];

Text(x) := WriteString(x);
Chapter(x) := WriteString(
"\\chapter{" : x : "}
");
Section(x) := WriteString(
"\\section{" : x : "}
");
SubSection(x) := WriteString(
"\\subsection*{" : x : "}
");

Itemize(body) :=
"\\begin{itemize}
" : body : "
\\end{itemize}";

Enumerate(body) :=
"\\begin{enumerate}
" : body : "
\\end{enumerate}";


Item(body) := "\\item{" : body : "}";

HtmlNewParagraph() := "

";

HtmlCommand(x) := "\\begin{quote}\\begin{verbatim}" : x : "\\end{verbatim}\\end{quote}
"; /* use "quote" and not "quotation" */

HtmlEmphasis(x) := "\\emph{" : x : "}";

/* decide whether to use "verb" or "texttt" based on whether the string contains any of the special characters #, _, \, &, {, }, ~ */

HasTexChars(x) := [
	Local(i);
	For (i:=1, i<=Length(x) And Not Contains(TexCharsToEscape, x[i]), i:=i+1) [];
	i<=Length(x);
];

HtmlTerm(x) := [
	If (HasTexChars(x),
	"\\verb|" : x : "|",
	"\\texttt{" : x : "}");
];

/* this will escape TeX symbols so that the result can be put into a section heading, i.e. no \verb|| */
HtmlTermHeading(x) := [
	HtmlTerm(x);	// for now
];

HtmlBreak() := "
";

Bodied("Item",2);	/* competes with prec. 7 of ":" */
Bodied("Book",60000);
Bodied("Title",60000);
Bodied("Author",60000);
Bodied("Chapter",60000);
Bodied("Section",60000);
Bodied("SubSection",60000);
Bodied("Text",60000);
Bodied("Blurb",60000);
Bodied("Itemize",2);
Bodied("Enumerate",2);

TeXFinishUp() := WriteString("
\\end{document}
");

IncludeFile(x) := Load(x);

HtmlLink(text, url, param1, param2) :=
  If (text = url,
	"\\verb|" : text : "|",
	text : " (Web URL: \\verb|" : url :
	  If(param1 != "", "#" : param1, "" )
	: "|)"
);

RuleBase("TeXMath", {x});
RuleBase("TeXMathD", {x});
HoldArg("TeXMath", x);
HoldArg("TeXMathD", x);
/* use <-- since := would redefine everything */
TeXMath(_x) <-- TeXForm(x);
TeXMathD(_x) <-- "$" : TeXForm(x) : "$";

/* stuff for refman */

ChapterIntro(string) := WriteString("
" : string : "
");
Bodied("ChapterIntro", 60000);

CmdDescription(cmd, desc) := WriteString("
\\section*{" : HtmlTermHeading(cmd) : " --- " : desc : "}
");

StandardLib() := WriteString("

\\begin{flushright} \\small Standard library \\end{flushright}

");

BuiltIn() := WriteString("
\\raggedleft {\\small Core function}
");

AddBody(string) := WriteString(string);

SeeAlso(list) := WriteString(
Nl() : PrintList(MapSingle("HtmlTerm", list), ", ") : Nl()
);

HtmlAnchor(string) := "";

Bodied("HtmlAnchor", 60000);
Topical(string) := WriteString(
"\\begin{quote}\\textbf{\\large " : string : "}\\end{quote}"
);
Bodied("Topical",60000);
