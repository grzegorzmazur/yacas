/* book2TeX - convert Yacas docs back to TeX format (Serge Winitzki)
	version 0.2 */

/* If one of these occurs inside an HtmlTerm block, it must be made \\verb| |*/
TexCharsToEscape := {"{", "}", "#", "_", "&", "\\", "^", "%"};

Book(x) := [	/* this is the first declaration in a file */
	WriteString("
\\documentclass[11pt]{report}

\\setlength{\\topmargin}{-1.2in}
\\setlength{\\oddsidemargin}{-0.5in}
\\setlength{\\evensidemargin}{-0.5in}
\\setlength{\\textwidth}{7.2in}
\\setlength{\\textheight}{10.0in}

\\begin{document}
\\title{" : x : "}
\\author{by the \\textsc{Yacas} team}
\\date{\\textsc{Yacas} version: " : Version() : "}
\\maketitle
\\tableofcontents
");
];

Text(x) := WriteString(x);
Chapter(x) := WriteString(
"\\chapter{" : x : "}
");
Section(x) := WriteString(
"\\section{" : x : "}
");
SubSection(x) := WriteString(
"\\subsection{" : x : "}
");

Itemize(body) :=
"\\begin{itemize}
" : body : "
\\end{itemize}";

Enumerate(body) :=
"\\begin{enumerate}
" : body : "
\\end{enumerate}";


Item(body) := "\\item{" : body : "}";

HtmlNewParagraph() := "

";

HtmlCommand(x) := "\\begin{quote}\\begin{verbatim}" : x : "\\end{verbatim}\\end{quote}
"; /* use "quote" and not "quotation" */

HtmlEmphasis(x) := "\\emph{" : x : "}";

/* decide whether to use "verb" or "texttt" based on whether the string contains any of the special characters #, _, \, &, {, } */
HtmlTerm(x) := [
	Local(i);
	For (i:=1, i<=Length(x) And Not Contains(TexCharsToEscape, x[i]), i:=i+1) [];
	If (i<=Length(x),
	"\\verb|" : x : "|",
	"\\texttt{" : x : "}");
];

HtmlBreak() := "
";

Bodied("Item",2);	/* competes with prec. 7 of ":" */
Bodied("Book",60000);
Bodied("Title",60000);
Bodied("Author",60000);
Bodied("Chapter",60000);
Bodied("Section",60000);
Bodied("SubSection",60000);
Bodied("Topical",60000);
Bodied("Text",60000);
Bodied("Blurb",60000);
Bodied("Itemize",2);
Bodied("Enumerate",2);

TeXFinishUp() := WriteString("
\\end{document}
");

IncludeFile(x) := Load(x);

HtmlLink(text, url, param1, param2) :=
  If (text = url,
	"\\verb|" : text : "|",
	text : " (Web URL: \\verb|" : url :
	  If(param1 != "", "#" : param1, "" )
	: "|)"
);

TeXMath(x) := "$" : x : "$";
TeXMathD(x) := "$$" : x : "$$";
