//////////////////////////////////////////////////
/// Backends for 2D plotting
//////////////////////////////////////////////////

/// List of all defined backends and their symbolic labels.
/// Add any new backends here 
Plot2D'outputs() := {
	{"default", "gnuplot"},
	{"data", "Plot2D'data"},
	{"datafile", "Plot2D'datafile"},
	{"gnuplot", "Plot2D'gnuplot"},
	{"plotutils", "Plot2D'plotutils"},
};

/*
	How backends work:
	Plot2D'<backend>(values, options'hash)
	options'hash is a hash that contains all plotting options:
	["xrange"] - a list of {x1, x2}, ["xname"] - name of the variable to plot, ["yname"] - array of string representations of the function(s), and perhaps other options relevant to the particular backend.
	{values} is a list of lists of pairs of the form {{{x1, y1}, {x2, y2}, ...}, {{x1, z1}, {x2, z2}, ...}, ...} corresponding to the functions y(x), z(x), ... to be plotted. The abscissa points x[i] are not the same for all functions.
	The backend should prepare the graph of the function(s). The "datafile" backend Plot2D'datafile(values, options'hash) may be used to output all data to file(s), in which case the file name should be given by the value options'hash["filename"]. Multiple files are created with names obtained by appending numbers to the filename.
	Note that the "data" backend does not do anything and simply returns the data.
	The backend Plot2D'datafile takes care not to write "Infinity" or "Undefined" data points (it just ignores them). Custom backends should either use Plot2D'datafile or take care of this themselves.
*/

/// trivial backend: return data list (do not confuse with Plot2D'get'data() defined in the main code which is the middle-level plotting routine)
Plot2D'data(values_IsList, _options'hash) <-- values;

/// backend: display graph using gnuplot
Plot2D'gnuplot(values_IsList, _options'hash) <--
[
  Local(item, dirBase, filename, filename'list);
  dirBase := "/tmp/plot.tmp/";
  filename := "data";
  // the "filename" option should not be used
  options'hash["filename"] := dirBase : filename;
  // create directory if it does not exist (unix specific)
  SystemCall("test -d " : dirBase : "|| mkdir -p " : dirBase);
  // write all data to file(s) and save the filenames
  filename'list := Plot2D'datafile(values, options'hash);
  // prepare the control file for gnuplot
  ToFile(dirBase : "gnuplot.in")
  [
	// 'set xrange [-5:5]'
    WriteString("set xrange [");
	Write(options'hash["xrange"][1]);
	WriteString(" : ");
	Write(options'hash["xrange"][2]);
	WriteString("]" : Nl());
	// 'plot "data1" title "y(x)" with lines, "data2" title "z(x)" with lines'
	WriteString("plot ");
	For(item:=1, item<=Length(values), item++)
	[
		WriteString("\"" : filename'list[item] : "\" title \"" : options'hash["yname"][item] : "\" with lines");
		// add a comma if necessary
		If(item<Length(values), WriteString(", "));
	];
  ];
  SystemCall("gnuplot -persist " : dirBase : "gnuplot.in");
];

/// backend: use "graph" from GNU plotutils
Plot2D'plotutils(values_IsList, _options'hash) <--
[
  Local(item, dirBase, filename, filename'list);
  dirBase := "/tmp/plot.tmp/";
  filename := "data";
  // the "filename" option should not be used
  options'hash["filename"] := dirBase : filename;
  // create directory if it does not exist
  SystemCall("test -d " : dirBase : "|| mkdir -p " : dirBase);
  // write all data to file(s) and save the filenames
  filename'list := Plot2D'datafile(values, options'hash);
  // build command line
  SystemCall(
  	"graph -T X --bitmap-size 640x480 -m 1 -C -h 0.54 -w 0.72 -r 0.15 -u 0.1"
    : " -x " : (ToString()Write(options'hash["xrange"][1], options'hash["xrange"][2]))
    : " -X " : (ToString()Write(options'hash["xname"]))
	: " -Y \"" : PrintList(options'hash["yname"]) : "\" "
	: PrintList(filename'list, " ")
  );
];

/// backend: write data into file.
/// Filename must be specified in options as options'hash["filename"].
/// Returns the list of created filenames
/// This backend is used by other backends to write data to files.
Plot2D'datafile(values_IsList, _options'hash) <--
[
	Local(item, func, filename, filename'list);
	filename'list := {};
	For(func:=1, func<=Length(values), func++)
	[
		filename := options'hash["filename"]:String(func);
		DestructiveAppend(filename'list, filename);
		ToFile(filename) ForEach(item, values[func]) WriteDataItem(item, options'hash);
		If(Verbose, Echo( "Plot2D'datafile: created file '" : filename :  "'"), True);
	];
	filename'list;
];

/// service function. WriteDataItem({1,2,3}, {}) will output "1 2 3" on a separate line.
/// Writes data points to the current output stream, omits non-numeric values.
WriteDataItem(tuple_IsList, _options'hash) <--
[
  Local(item);
  If(	// do not write anything if one of the items is not a number
  	IsNumericList(tuple),
	ForEach(item,tuple)
	[
		Write(item);
		Space();
	]
  );
  NewLine();
];

