
/* This file contains some math functions that can be defined based on the
  BigNumber API. This file should only use the features supported by the
  compiler, as it gets compiled to a plugin for speed.  
 */

Defun("Trigonometry",{x,i,sum,term})
[
  Local(x2,orig,eps);
  Set(x2,MathMultiply(x,x));
  Set(eps,MathPower(10,MathSubtract(0,GetPrecision())));
  While(GreaterThan(MathAbs(term),eps))
  [
    Set(term,MathMultiply(term,x2));
    Set(i,MathAdd(i,1.0));
    Set(term,MathDivide(term,i));
    Set(i,MathAdd(i,1.0));
    Set(term,MathDivide(MathSubtract(0,term),i));
    Set(sum, MathAdd(sum, term));
  ];
  sum;
];

Defun("MathSin",{x})Trigonometry(x,1.0,x,x);
Defun("MathCos",{x})Trigonometry(x,0.0,1.0,1.0);
Defun("MathTan",{x})MathDivide(MathSin(x),MathCos(x));

Defun("MathArcSin",{int1})
[
  Local(result,eps);
	Set(result,FastArcSin(int1));
  Local(x,q,s,c);
  Set(q,MathSubtract(MathSin(result),int1));
  Set(eps,MathPower(10,MathSubtract(0,GetPrecision())));
  While(GreaterThan(MathAbs(q),eps))
  [
		Set(s,MathSubtract(int1,MathSin(result)));
    Set(c,MathCos(result));
    Set(q,MathDivide(s,c));
    Set(result,MathAdd(result,q));
  ];
  result;
];



Defun("MathExp",{x})
[
  Local(i,aResult,term,eps);
  // Exp(x)=Sum(i=0 to Inf)  x^(i) /(i)!
  // Which incrementally becomes the algorithm:
  //
  // i <- 0
  Set(i,0);
  // sum <- 1
  Set(aResult,1.0);
  // term <- 1
  Set(term,1.0);
  Set(eps,MathPower(10,MathSubtract(0,GetPrecision())));
  // While (term>epsilon)
  While(GreaterThan(MathAbs(term),eps))
  [
    //   i <- i+1
    Set(i,MathAdd(i,1));
    //   term <- term*x/(i)
    Set(term,MathDivide(MathMultiply(term,x),i));
    //   sum <- sum+term
    Set(aResult,MathAdd(aResult,term));
  ];
  aResult;
];



