# Process this file with autoconf to produce a configure script.
AC_INIT(src/yacasmain.cpp)
AM_CONFIG_HEADER(config.h)

PACKAGE=yacas
MAJOR=1
MINOR=0
MICRO=53rev3
RELEASE=1

VERSION=$MAJOR.$MINOR.$MICRO
AM_INIT_AUTOMAKE($PACKAGE, $VERSION)
AC_SUBST(RELEASE)

# Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_RANLIB
AC_CHECK_PROG(have_perl, perl, yes, no)
AC_CHECK_PROG(have_latex, latex, yes, no)
AC_CHECK_PROG(have_makeindex, makeindex, yes, no)
AC_CHECK_PROG(have_pdflatex, pdflatex, yes, no)
AC_CHECK_PROG(have_dvips, dvips, yes, no)
AC_CHECK_PROG(have_gzip, gzip, yes, no)

# libtool support
#AC_LIBTOOL_DLOPEN
#AM_PROG_LIBTOOL


# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h sys/ioctl.h sys/socket.h sys/time.h sys/types.h)

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_HEADER_TIME

# Checks for library functions.
AC_CHECK_FUNCS(select socket strtod)

# checks for libraries
AC_CHECK_LIB(dl,dlopen,LIBS="$LIBS -ldl -rdynamic",,-rdynamic)

AC_FUNC_ACCEPT_ARGTYPES
TYPE_SOCKLEN_T


# Choice of arbitrary precision library
AC_ARG_ENABLE(gmp, [  --enable-gmp            use GNU MP [default=no]])
if test "$enable_gmp" = "yes" ; then
	AC_CHECK_LIB(gmp, __gmpz_init, have_gmp=yes, have_gmp=no)
	if test "$have_gmp" = "no" ; then
		AC_MSG_ERROR([GNU MP library not found])
	fi
	NUMBERS_LIB="libgmpnumbers.a"
	NUMBERS_LDFLAGS="-lgmp -lm"
else
	NUMBERS_LIB="libyacasnumbers.a"
	NUMBERS_LDFLAGS="-lm"
fi
AC_SUBST(NUMBERS_LIB)
AC_SUBST(NUMBERS_LDFLAGS)

# Generate HTML documentation?
AC_ARG_ENABLE(html-doc, [  --disable-html-doc      do not generate HTML documentation])
if test "x$enable_html_doc" = x; then
	enable_html_doc=$have_perl
	if test "x$have_perl" = xno; then
		AC_MSG_WARN([
		Perl was not found. Therefore, the generation of 
		HTML documentation has been disabled.])
	fi
elif test "x$enable_html_doc" = xyes; then
	if test "x$have_perl" = xno; then
		AC_MSG_ERROR([
		Generation of HTML documentation was explicitly 
		requested. However, this requires Perl, which
		was not found.])
	fi
fi
if test "x$enable_html_doc" = xyes; then
	BOOKS_HTML=books.html
	INSTALL_HTML=install-html
fi
AC_SUBST(BOOKS_HTML)
AC_SUBST(INSTALL_HTML)

# Generate PS documentation?
AC_ARG_ENABLE(ps-doc, [  --enable-ps-doc         generate PostScript documentation])
if test "x$enable_ps_doc" = xyes; then
	if test "x$have_perl" = xno || test "x$have_latex" = xno \
	   || test "x$have_makeindex" = xno || test "x$have_dvips" = xno \
	   || test "x$have_gzip" = xno; then
		AC_MSG_ERROR([need perl, latex, makeindex, dvips, and gzip to generate Postscript])
	fi
	PS_DOCS=ps-docs
	INSTALL_PS=install-ps
fi
AC_SUBST(PS_DOCS)
AC_SUBST(INSTALL_PS)

# Generate PDF documentation?
AC_ARG_ENABLE(pdf-doc, [  --enable-pdf-doc        generate PDF documentation])
if test "x$enable_pdf_doc" = xyes; then
	if test "x$have_perl" = xno || test "x$have_pdflatex" = xno \
	   || test "x$have_makeindex" = xno || test "x$have_dvips" = xno; then
		AC_MSG_ERROR([need perl, pdflatex, and makeindex to generate PDF docs])
	fi
	PDF_DOCS=pdf-docs
fi
AC_SUBST(PDF_DOCS)

# Choice of directories
AC_ARG_WITH(script-dir, [  --with-script-dir=DIR   program scripts in DIR [DATADIR/yacas]])
if test -n "$with_script_dir" ; then
	scriptdir=$with_script_dir
else
	scriptdir=${datadir}/yacas/
fi
AC_SUBST(scriptdir)

AC_ARG_WITH(html-dir, [  --with-html-dir=DIR     HTML documentation in DIR [SCRIPTDIR/documentation]])
if test -n "$with_html_dir" ; then
	htmldir=$with_html_dir
else
	htmldir=${scriptdir}/documentation/
fi
AC_SUBST(htmldir)

AC_ARG_WITH(ps-dir, [  --with-ps-dir=DIR       PostScript documentation in DIR [HTMLDIR]])
if test -n "$with_ps_dir" ; then
	psdir=$with_ps_dir
else
	psdir=${htmldir}
fi
AC_SUBST(psdir)

# All done
AC_OUTPUT(yacas.spec
	Makefile
	docs/Makefile
	inprogress/Makefile
	proteus/Makefile
	YacasNotebook/Makefile
	essays/Makefile
	manualmaker/Makefile
	plugins/Makefile
	scripts/Makefile
	colorcode/Makefile
	ramscripts/Makefile
	epoc/Makefile
	tests/Makefile
        manmake/Makefile
        compile/Makefile
        embed/Makefile
	src/Makefile)
