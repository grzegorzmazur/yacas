!!Fun with derivatives part 1: defining the concept of a derivative in <i>Yacas</i>

This is the first article in a series of articles on doing calculations involving derivatives in <i>Yacas</i>.
Defining a function that can determine the derivative of a function has been a classic example of something
you can do easily in languages similar to <i>Yacas</i>, and provides a good starting point to show off
the capabilities <i>Yacas</i> has with respect to defining transformation rules.

You need surprisingly few lines of code to actually define the operation of taking a derivative. We will develop 
the code that can be used in <i>Yacas</i> in this article for instructional purposes. <i>Yacas</i> naturally already
knows how to take the derivative of a function. One can take a derivative through the {D} function.
But it is still a good example that demonstrates how to use transformation rules in <i>Yacas</i>, as very few lines
of code are needed and most people readily know the transformation rules by heart (the transformation rules lend themselves
quite well to implementation in a computer program). It is also an example of a function
where the answer is not a number but a more complex thing, another function.

We will define the function dd, which will take two arguments. The first is the variable, the second the function 
to take the derivative off (with respect to the variable given as first argument).

First we can tell the system that taking the derivative of a variable with respect to itself (eg. taking the derivative of
{x} with respect to {x}) is one:
{{
dd(_x,_x) &lt;-- 1;
}}
The pattern on the left side has two matchable places named {_x}. 
This instructs <i>Yacas</i> to check that they are the same.

Next we tell the system that if you take a derivative of a function with respect to a variable that that function does not depend
on, the result is zero. Taking the derivative of say {f(y)} with respect to 
{x} would yield the result zero, assuming that {f(y)} does
not depend on {x}.
{{
dd(_x,y_IsFreeOf(x)) &lt;-- 0;
}}
Note the construct {y_IsFreeOf(x)}. <i>Yacas</i> internally transforms that construct to 
mean {IsFreeOf(x,y)}. So this pattern matches only
if the expression to take the derivative off does not depend on the variable to take the derivative with respect of. The result is 
zero in that case.

Taking derivatives is a linear operation. The derivative of an expression that adds two expressions is the derivative of the one plus
the derivative of the other. This is summarized in the following rules:
{{
dd(_x,_a + _b)  &lt;-- dd(x,a)+dd(x,b);
dd(_x,_a - _b)  &lt;-- dd(x,a)-dd(x,b);
dd(_x,   - _b)  &lt;--        -dd(x,b);
}}

Taking the derivative of a function to the power of a constant is also a well-known transform. Taking the derivative of 
{x^n} with respect to
{x} yields {n*x^(n-1)}. This can be generalized to 
taking the derivative of {f(x)^n} by applying the chain rule: 
{d(x,f(g(x)))} is {d(x,g)*d(g,f)},
symbolically. We will embed the chain rule within the transformation rules:

{{
dd(_x,_y ^ z_IsFreeOf(x)) &lt;-- dd(x,y)*z*y^(z-1);
}}

Taking the derivative of an expression that consists of one expression multiplied by the other also has a commonly known transformation rule:
{{
dd(_x,_a * _b)  &lt;-- a*dd(x,b)+dd(x,a)*b;
}}
And so does division:
{{
dd(x,_y / _z) &lt;-- (z*dd(x,y)-y*dd(x,z))/z^2;
}}

Finally we add the derivatives of a few well-known analytic functions:
{{
dd(_x,Sin(_y)) &lt;--  dd(x,y)*Cos(y);
dd(_x,Cos(_y)) &lt;-- -dd(x,y)*Sin(y);
dd(_x,Exp(_y)) &lt;--  dd(x,y)*Exp(y);
dd(_x,Sqrt(_y)) &lt;-- dd(x,y)/(2*Sqrt(y));
}}
This list can be expanded further of course.

For convenience we also defined a function {dd(x,n,f)}, which takes the derivative 
of {f} with respect to {x} 
{n} times. The function calls itself
{n} times recursively until {n} is zero (in which 
case it returns the expression unchanged).

{{
dd(_x,0,_f) <-- f;
dd(_x,n_IsPositiveInteger,_f) <-- dd(x,n-1,dd(x,f));
}}

If you click on the button labeled "Try out" above this text you will be directed to the <i>Yacas</i> calculation center.
Here you can try out the function {dd(x,f)} that is developed here. Clicking on the 
button labeled "Example" will result in a pre-determined example being typed in for you. The "Edit" button allows you to
edit the code, modifying it before re-running the code.

There are sometimes different ways of representing the same result. Compare for example the result of calculating {dd(x,n,Sqrt(x))}
with {dd(x,n,x^0.5)} (we use {0.5} here and not {(1/2)}, because <i>Yacas</i> does some
standard simplifications, one of which is converting {x^(1/2)} to {Sqrt(x)}, something we don't want for this example). 
The length of the one result is much longer than the other, although mathematically they are identical. 


