/*
 TESTS:
 - random-test code for roots, be generating random roots and
   multiplicities.
 - find an example where bisection is needed, or better, a group
   of examples where bisection is needed, for tests
 - GarbageCollect in TryRandomPoly causes some corruption, as is
   visible when turning show file/line on.
*/

Builtin'Precision'Set(5);

VerifyZero(x) := (Abs(x)<10^ -Builtin'Precision'Get());

TryRandomPoly(deg,coefmin,coefmax):=
[
  //GarbageCollect();
  Local(coefs,p,roots,px);
  coefs:=Table(MathFloor(coefmin+Random()*(coefmax-coefmin)),i,1,deg+1,1);
  p:=Add(coefs*x^(0 .. deg));
  p:=Rationalize(p);
//Echo("Test polynom ",p);
  Verify(Max(Abs(coefs))<=MaximumBound(p));
  Verify(Min(Abs(coefs))>MinimumBound(p));
//Echo("bounds ",BoundRealRoots(p));
  roots:=FindRealRoots(p);
//Echo("roots ",roots);
  px := (p Where x==x);
  Verify(px . px < 0.01);
];
TryRandomRoots(deg,coefmin,coefmax):=
[
  //GarbageCollect();
  Local(coefs,p,roots,px,mult);
  coefs:=RemoveDuplicates(Table(MathFloor(coefmin+Random()*(coefmax-coefmin)),i,1,deg+1,1));
  deg:=Length(coefs)-1;
  mult:=1+Abs(Table(MathFloor(coefmin+Random()*(coefmax-coefmin)),i,1,deg+1,1));
  p:=Factorize((x-coefs)^mult);
  p:=Rationalize(p);
  Echo("Test polynom ",p);
  Echo("minimum ",MinimumBound(p));
  Echo("maximum ",MaximumBound(p));
  Echo("bounds ",BoundRealRoots(p));
  roots:=FindRealRoots(p);
  Echo("roots ",roots);
  Verify(Length(roots) = Length(coefs));
  Verify(Length(RemoveDuplicates(roots)) = Length(coefs));
  px := (p Where x==x);
  Verify(px . px < 0.01);
];

[
  Local(p);
  p := FindRealRoots((x+2)^9*(x-4)^5*(x-1)^4)-{-2.,1.,4.};
  Verify(VerifyZero(p . p),True);
];

/*TODO
TryRandomRoots(3,-10,10);
TryRandomRoots(3,-10,10);
TryRandomRoots(5,5,1000);
TryRandomRoots(5,5,1000);
*/

// Bounds on coefficients
Verify(MinimumBound(4),-Infinity);
Verify(MaximumBound(4),Infinity);

// NumRealRoots
Verify(NumRealRoots(x^2-1),2);
Verify(NumRealRoots(x^2+1),0);

[
  Local(p);
  p:=FindRealRoots(Expand((x*(x-10)^3*(x+2)^2)))-{0,-2.,10.};
  Verify(VerifyZero(p . p),True);
];
Verify(FindRealRoots((x^2+20)*(x^2+10)),{});
Verify(NumRealRoots((x^2+20)*(x^2+10)),0);

// Simple test on Squarefree
TestYacas(Monic(SquareFree((x-1)^2*(x-3)^3)),Monic((x-1)*(x-3)));

// Check the rare case where the bounds finder lands on
// exactly a root
[
  Local(p);
  p:=FindRealRoots((x+4)*(x-6),1,7)-{-4.,6.};
  Verify(VerifyZero(p . p),True);
];

[
  Local(p);

  p:=Expand((x-3.1)*(x+6.23));
  p:=FindRealRoots(p)-{-6.23,3.1};
  Verify(VerifyZero(p . p),True);
];

Verify(Builtin'Precision'Get(),5);
[
  Local(res);
  res:=FindRealRoots(Expand((x-3.1)*(x+6.23)))-{-6.23,3.1};
  Verify(VerifyZero(res . res) , True);
];

TryRandomPoly(5,5,1000);
TryRandomPoly(5,5,1000);
TryRandomPoly(5,5,1000);
TryRandomPoly(5,5,1000);
TryRandomPoly(5,5,1000);
TryRandomPoly(5,5,1000);
