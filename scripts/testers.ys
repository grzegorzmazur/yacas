
/* Functions that aid in testing */



/* Logging functions */
curline:=0;

Function("StartTests",{})
[
  curline:=0;
];
Function("NextTest",{aLeft})
[
 curline++;
WriteString("
":String(curline):". ":aLeft:" : "
           );
  NewLine();
];
Function("Testing",{aLeft})
[
 WriteString(aLeft); NewLine();
];


Function("Verify",{aLeft,aRight})
[
  If (Not(Equals(Eval(aLeft),aRight)),
    [
      WriteString("***** ");
      Write(aLeft);
      WriteString(" does not evaluate to ");
      Write(aRight);
      NewLine();
      False;
    ],
    True
  );
];
/*
HoldArg("Verify",aLeft);
HoldArg("Verify",aRight);
*/

f1(x,n,m):=(x^n-1)*(x^m-1);
f2(x,n,m):=x^(n+m)-(x^n)-(x^m)+1;

VerifyArithmetic(x,n,m):=
[
  WriteString("Checking arithmetic with ");
  Write(x);Space();
  Write(n);Space();
  Write(m);NewLine();
  Verify(f1(x,n,m),f2(x,n,m));
];
RandVerifyArithmetic(_n)<--
[
 While(n>0)
 [
   n--;
   VerifyArithmetic(MathFloor(300*Random()),MathFloor(80*Random()),MathFloor(90*Random()));
 ];
];

VerifyDiv(_u,_v) <--
[
  Local(q,r);
  q:=Div(u,v);
  r:=Mod(u,v);

  Verify(Expand(u),Expand(q*v+r));
];

10 # EchoInternal(string_IsString) <--
[
  WriteString(string);
];

20 # EchoInternal(_item) <--
[
  Write(item);Space();
];

10 # Echo(list_IsList)<--
[
  ForEach(item,list) EchoInternal(item);
  NewLine();
];
20 # Echo(_item)<--
[
  EchoInternal(item);
  NewLine();
];


Function("BenchCall",{expr}) 
[
  Echo({"In&gt ",expr});
  WriteString("<font color=ff0000>");
  Eval(expr);
  WriteString("</font>");
  True;
];
HoldArg("BenchCall",expr);

Function("BenchShow",{expr}) 
[
  Echo({"In&gt ",expr});
  WriteString("<font color=ff0000> ");
  Echo({"Out&gt ",Eval(expr),"</font>"});
  True;
];
HoldArg("BenchShow",expr);




/* Testing Yacas functionality by checking expressions against correct
   answer.
 */

Function ("TestYacas", {expr, ans})
[
        Local(difference);
        difference := Simplify(expr-ans);
        If (difference = 0, True, Prog(Write(expr), WriteString(" is not
equal to "), Write(ans), NewLine(), WriteString("The difference is "),
Write(difference), NewLine(), False));
];

HoldArg("TestYacas", expr);
HoldArg("TestYacas", ans);







